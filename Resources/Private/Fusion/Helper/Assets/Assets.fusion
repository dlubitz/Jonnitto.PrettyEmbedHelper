prototype(Jonnitto.PrettyEmbed:Assets) < prototype(Neos.Fusion:Component) {
    @propTypes {
        player = ${PropTypes.oneOf(['YouTube', 'Vimeo', 'Video', 'Audio', 'Media']).isRequired}
        gdprHandling = ${PropTypes.oneOf([true, false, 'popup'])}
        includeAlpineJsCheck = ${PropTypes.bool}
        inBackend = ${PropTypes.bool}
    }

    includeAssets = ${Configuration.setting('Jonnitto.PrettyEmbed.includeAssets')}
    includeAlpineJsCheck = ${Configuration.setting('Jonnitto.PrettyEmbed.includeAlpineJsCheck')}
    inBackend = ${node.context.inBackend}

    // This decides which file to load
    player = null
    player.@process.fallback = ${value == 'YouTube' || value == 'Vimeo' ? value : 'Media'}

    includeFrontendScripts = ${!this.inBackend && this.includeAssets.js}

    renderer = Neos.Fusion:Join {
        generalCSS = Jonnitto.PrettyEmbed:Assets.Fragment.Style {
            @if.set = ${props.includeAssets.css}
            filename = 'Main.css'
        }
        backendVideoCSS = Jonnitto.PrettyEmbed:Assets.Fragment.Style {
            @if.set = ${props.inBackend && props.video}
            filename = 'Backend.Video.css'
        }
        frontendScripts = Neos.Fusion:Join {
            @if.set = ${props._includeFrontendScripts}
            gdprPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.gdprHandling == true}
                filename = 'Gdpr.js'
            }
            magicPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.player == 'Media' || props.gdprHandling != 'Popup'}
                filename = 'Magic.js'
            }
            popupPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.gdprHandling}
                filename = 'Popup.js'
            }
            pluginBasedOnPlayer = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.player}
                filename = ${props.player + '.js'}
            }
            alpineJs = Jonnitto.PrettyEmbed:Assets.Fragment.Script {
                @if.set = ${props.includeAlpineJsCheck}
                filename = 'AlpineCheck.js'
            }
        }
    }
}
