prototype(Jonnitto.PrettyEmbed:Assets) < prototype(Neos.Fusion:Component) {
    @propTypes {
        player = ${PropTypes.oneOf(['YouTube', 'Vimeo', 'Video', 'Audio']).isRequired}
        gdprHandling = ${PropTypes.oneOf([true, false, 'popup'])}
        includeAlpineJsCheck = ${PropTypes.boolean}
        inBackend = ${PropTypes.boolean}
    }

    includeAssets = ${Configuration.setting('Jonnitto.PrettyEmbed.includeAssets')}
    includeAlpineJsCheck = ${Configuration.setting('Jonnitto.PrettyEmbed.includeAlpineJsCheck')}
    inBackend = ${node.context.inBackend && node.context.currentRenderingMode.edit}

    // This decides which file to load
    player = null

    renderer = Neos.Fusion:Join {
        generalCSS = Jonnitto.PrettyEmbed:Assets.Fragment.Style {
            @if.set = ${props.includeAssets.css}
            filename = 'Main.css'
        }
        backendJS = Jonnitto.PrettyEmbed:Assets.Fragment.Script {
            @if.set = ${props.inBackend}
            inline = true
            filename = 'Backend.js'
        }
        frontendScripts = Neos.Fusion:Join {
            @if.set = ${props.includeAssets.js}
            gdprPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.gdprHandling == true}
                filename = 'Consent.js'
            }
            magicPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.player == 'Video' || props.player == 'Audio' || props.gdprHandling != 'Popup'}
                filename = 'Magic.js'
            }
            popupPlugin = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.gdprHandling}
                filename = 'Popup.js'
            }
            pluginBasedOnPlayer = Jonnitto.PrettyEmbed:Assets.Fragment.Module {
                @if.set = ${props.player}
                filename = ${props.player == 'Video' || props.player == 'Audio' ? 'Media.js' : (props.player + '.js')}
            }
            alpineJs = Jonnitto.PrettyEmbed:Assets.Fragment.Script {
                @if.set = ${props.includeAlpineJsCheck}
                filename = 'AlpineCheck.js'
                slipstream = '//body'
            }
        }
    }
}
