prototype(Jonnitto.PrettyEmbedHelper:Assets) < prototype(Neos.Fusion:Component) {
    includeAssets = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.includeAssets')}
    openInNewWindow = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.openInNewWindow')}
    inBackend = ${node.context.inBackend}
    youtubeAPI = false
    vimeoAPI = false

    renderer = Neos.Fusion:Join {
        vimeoAPI = Neos.Fusion:Tag {
            @if.set = ${!props.inBackend && props.vimeoAPI}
            tagName = 'script'
            attributes {
                data-slipstream = true
                defer = true
                src = 'https://player.vimeo.com/api/player.js'
            }
        }
        youtubeAPI = Neos.Fusion:Tag {
            @if.set = ${!props.inBackend && props.youtubeAPI}
            tagName = 'script'
            attributes {
                data-slipstream = true
                defer = true
                src = 'https://www.youtube.com/iframe_api'
            }
        }
        generalCSS = Jonnitto.PrettyEmbedHelper:Assets.Fragment.Style {
            @if.set = ${props.includeAssets.css}
            filename = 'Main.css'
        }
        backendCSS = Jonnitto.PrettyEmbedHelper:Assets.Fragment.Style {
            @if.set = ${props.inBackend}
            filename = 'Backend.css'
        }
        backendJS = Jonnitto.PrettyEmbedHelper:Assets.Fragment.Script {
            @if.set = ${props.inBackend}
            filename = 'Backend.js'
        }
        liveJS = Jonnitto.PrettyEmbedHelper:Assets.Fragment.Script {
            @if.set = ${!props.inBackend && props.includeAssets.js}
            openYoutubeInNewWindow = ${props.openInNewWindow.youtube}
            openVimeoInNewWindow = ${props.openInNewWindow.vimeo}
            filename = 'Main.js'
        }
    }
}
