prototype(Jonnitto.PrettyEmbed:Presentation.Iframe) < prototype(Neos.Fusion:Component) {
    @propTypes {
        player = ${PropTypes.oneOf(['YouTube', 'Vimeo']).isRequired}
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
        id = ${PropTypes.string}
        label = ${PropTypes.string}
        href = ${PropTypes.string}
        embedHref = ${PropTypes.string}
        preview = ${PropTypes.string}
        poster = ${PropTypes.string}
        poster2x = ${PropTypes.string}
        posterSrcset = ${PropTypes.string}
        loadImageStrategy = ${PropTypes.oneOf(['lazy', 'eager', null, false])}
        aspectRatio = ${PropTypes.anyOf(PropTypes.string, PropTypes.float, PropTypes.integer)}
        setAspectRatioClass = ${PropTypes.boolean}
        preserveAspectRatio = ${PropTypes.boolean}
        fallbackAspectRatio = ${PropTypes.anyOf(PropTypes.string, PropTypes.float, PropTypes.integer)}
        controls = ${PropTypes.boolean}
        lightbox = ${PropTypes.boolean}
        wrapper = ${PropTypes.anyOf(PropTypes.string, PropTypes.boolean)}
        gdprHandling = ${PropTypes.oneOf([true, false, 'popup']).isRequired}
        loop = ${PropTypes.boolean}
        videoId = ${PropTypes.anyOf(PropTypes.string, PropTypes.integer)}

        # YouTube only
        playlistId = ${PropTypes.anyOf(PropTypes.string, PropTypes.integer)}

        # Vimeo only
        color = ${PropTypes.anyOf(PropTypes.string, PropTypes.boolean)}
    }

    loadImageStrategy = ${Configuration.setting('Jonnitto.PrettyEmbed.loadImageStrategy')}
    wrapper = ${Configuration.setting('Jonnitto.PrettyEmbed.wrapper')}
    gdprHandling = true
    controls = true

    preserveAspectRatio = true
    fallbackAspectRatio = '16 / 9'
    aspectRatio = null
    aspectRatio.@process.fallback =${value || this.fallbackAspectRatio}
    setAspectRatioClass = ${this.lightbox && this.preserveAspectRatio ? false : !!this.aspectRatio}

    @if {
        checkPlayer = ${this.player == 'YouTube' || this.player == 'Vimeo'}
        checkVimeo = ${this.player == 'Vimeo' ? (this.videoId && this.href && this.embedHref) : true}
        checkYouTube = ${this.player == 'YouTube' ? (this.videoId && this.href && this.embedHref) : true}
        checkPreviewImage = ${this.poster || this.preview}
    }
    _alwaysOpenInPopup = ${this.gdprHandling == 'popup'}

    renderer = afx`
        <Jonnitto.PrettyEmbed:Presenation.Wrapper wrapper={props.wrapper}>
            <div
                x-data
                x-prettyembedvimeo={AlpineJS.object({
                    gdpr:props.gdprHandling,
                    videoId: props.videoId,
                    color: Type.isString(props.color) ? props.color : null,
                    slim: !props.controls || null,
                    lightbox: props.lightbox || null,
                    loop: props.loop || null
                })}
                x-prettyembedvimeo.@if={props.player == 'Vimeo' && !props._alwaysOpenInPopup}
                x-prettyembedyoutube={AlpineJS.object({
                    gdpr:props.gdprHandling,
                    videoId: props.videoId || null,
                    playlistId: props.playlistId || null,
                    slim: !props.controls || null,
                    lightbox: props.lightbox || null,
                    loop: props.loop || null
                })}
                x-prettyembedyoutube.@if={props.player== 'YouTube' && !props._alwaysOpenInPopup}
                aria-label={props.label}
                id={props.id}
                class={Carbon.String.merge(props.class, BEM.modifier('jonnitto-prettyembed', String.toLowerCase(props.player)), props.setAspectRatioClass && 'jonnitto-prettyembed-ratio')}
                style={props.aspectRatio}
            >
                <Jonnitto.PrettyEmbed:Assets
                    player={props.player}
                    gdprHandling={props.gdprHandling}
                />
                <Jonnitto.PrettyEmbed:Presentation.Button.Play
                    @if={!props._alwaysOpenInPopup}
                    show={props.controls && !props.lightbox ? "!loaded" : null}
                    onClick="load"
                    href={props.href}
                />
                <Jonnitto.PrettyEmbed:Presentation.Button.Play
                    @if={props._alwaysOpenInPopup}
                    href={props.href}
                    popup={props.embedHref}
                />
                <Jonnitto.PrettyEmbed:Presentation.Button.Pause
                    @if={!props.controls && !props.lightbox && !props._alwaysOpenInPopup}
                />
                <Jonnitto.PrettyEmbed:Presentation.GdprConsent
                    @if={props.gdprHandling == true}
                    href={props.href}
                    embedHref={props.embedHref}
                />
                <Jonnitto.PrettyEmbed:Presentation.Preview
                    show={props._alwaysOpenInPopup || props.lightbox ? null : "!loaded"}
                    content={props.preview}
                    label={props.label}
                    src={props.poster}
                    src2x={props.poster2x}
                    srcset={props.posterSrcset}
                    loadImageStrategy={props.loadImageStrategy}
                />
                <Jonnitto.PrettyEmbed:Presentation.YouTubeTarget
                    @if={!props.lightbox}
                    player={props.player}
                    controls={props.controls}
                />
                <Jonnitto.PrettyEmbed:Presentation.Lightbox
                    @if={props.lightbox && !props._alwaysOpenInPopup }
                    aspectRatio={props.aspectRatio}
                    player={props.player}
                    playButton={!props.controls}
                    pauseButton={!props.controls}
                />
            </div>
        </Jonnitto.PrettyEmbed:Presenation.Wrapper>
    `
}
